// License Information:
// House of Cards
// Configuration Link-ups
// 
// 
// 
// (c) Jeroen P. Broks, 2024
// 
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.
// 
// Please note that some references to data like pictures or audio, do not automatically
// fall under this licenses. Mostly this is noted in the respective files.
// 
// Version: 24.03.07
// End License Information

Module

#use "Libs/SCI/Sys"
#use "Libs/SCI/JCR6"
#use "Libs/SCI/GINIE"
#use "Libs/SCI/SString"
#use "Libs/SCI/Graphics"

#use "Libs/Rosetta"


PLUA _Back
ReadOnly Var CFG = GINIE.Obtain("GLOBALCONFIG")
	
Get String Language 
	Return CFG.Value("Rosetta","Language")
End

Set String Language
	//CFG.Value("Rosetta","Language",Value)
	Rosetta.Lang = Value
End

Void ___ForceBaseBackgroundSettings()
	CFG.Value("Background","Entry","GFX/Backgrounds/Tile/Green.jpg")
	CFG.Value("Background","Draw","Tile")
	CFG.Value("Background","LastPlatform",Sys.Platform)
End

Void ___BaseBackgroundSettings()
	CFG.NewValue("Background","Entry","GFX/Backgrounds/Tile/Green.jpg")
	CFG.NewValue("Background","Draw","Tile")
	CFG.NewValue("Background","LastPlatform",Sys.Platform)
	If Sys.Platform!=CFG.Value("Background","LastPlatform")
		Sys.Annoy("Warning!\n\nIt seems the configuration file\n was copied from "..CFG.Value("Background","LastPlatform").." to "..Sys.Platform.."\n\nSafety require to reset the background settings\nas a result!")
		___ForceBaseBackgroundSettings()
	End
	If Upper(CFG.Value("Background","Entry"))=="*EXTERN*"
		Sys.Annoy("External backgrounds not yet supported.\nI must revert to base background settings")
		___ForceBaseBackgroundSettings()
	End
End

Get String BackEntry
	___BaseBackgroundSettings()
	return CFG.Value("Background","Entry")
End

Set String BackEntry
	Lua.Graphics.KillImage("BACKGROUND")
	CFG.Value("Background","Entry",Value)
	_Back = nil
End

Get String BackName
	___BaseBackgroundSettings()
	If Upper(CFG.Value("Background","Entry"))=="*EXTERN*"
		Return "EXTERNAL: "..CFG.Value("Background","Extern_"..Sys.Platform)
	End
	Return StripExt(StripDir(BackEntry))
End

Get String BackDraw
	___BaseBackgroundSettings()
	Return CFG.Value("Background","Draw")
End

Set String BackDraw
	CSayF("BackDraw set to: \"%s\"!",Value)
	CFG.Value("Background","Draw",Value)
End

Get Var Background
	___BaseBackgroundSettings()
	_Back = _Back || LoadNewImage(BackEntry,"BACKGROUND")
	Return _Back
End	

// I know! Odd to place here, but it's only ONE function, nearly all substates need it anyway, and it saves me a #use clause all the time!
Global Void DrawBackground()
	switch BackDraw
		Case "Draw" "Center"
			Background.Draw((Graphics.Width div 2)-(Background.width div 2),(Graphics.Height div 2)-(Background.Height div 2))
		case "Tile" "Stretch"
			Background[BackDraw](0,0,Graphics.width,Graphics.height)
		default
			Cls()
	End
End

Get String GlobDeck
	CFG.NewValue("Deck","Global","Base Deck")
	Return CFG.Value("Deck","Global")
End


Get String BackSide
	string Ret = CFG.Value("Deck","BacKSide")
	If !Ret || Ret==""
		plua BSD = JCR6.Dir("GFX/Backsides")
		CFG.Value("Deck","BacKSide",BSD[math.random(0,len(BSD)-1)])
		Ret = CFG.Value("Deck","BacKSide")
		CSayF("No backside present. Selected one: %s",Ret)		
	End
	Return Ret
End

Set String BackSide; CFG.Value("Deck","BacKSide",Value); End
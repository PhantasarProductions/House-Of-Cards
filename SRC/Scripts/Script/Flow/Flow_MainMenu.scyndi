// License Information:
// House of Cards
// Main Menu
// 
// 
// 
// (c) Jeroen P. Broks, 2024
// 
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.
// 
// Please note that some references to data like pictures or audio, do not automatically
// fall under this licenses. Mostly this is noted in the respective files.
// 
// Version: 24.04.03
// End License Information
Script

#use "Libs/SCI/Sys"
#use "Libs/SCI/Events"
#use "Libs/SCI/Graphics"

#use "Script/Use/Gen/Muis"
#use "Script/Use/Gen/RText"
#use "Script/Use/Gen/Config"

Bool Always(); Return True; End

Class MenuItem
	Static Int Cnt = 0
	Static Var First = Nil
	Static Var Last = Nil
	
	Var Next = Nil
	Var Prev = Nil
	
	ReadOnly String RTag
	ReadOnly Delegate Action
	ReadOnly Delegate Allow
	ReadOnly Int ID
		
	Get Int Y
		Return 200+(ID*35)
	End
	
	Static ReadOnly Int X = Graphics.Width div 2
	
	Constructor(String _T,_Act,_All)
		ID     = Cnt; Cnt++ 
		RTag   = _T
		Action = _Act
		Allow  = _All || Always
		If Last
			Last.Next = Self
			Prev = Last
		End
		First  = First || Self
		Last   = Self
		CSayF("Menu item %s created",RTag)
	End
	
	Destructor
		CSayF("Menu item %s destroyed",RTag)
	End
End

Void Play()
	Sys.LoadNewState("FLOW_SELECTGAME","Script/Flow/Flow_SelectGame.lbc")
	Sys.CurrentFlow = "SELECTGAME"

End

Void Config()
	Sys.LoadNewState("FLOW_CONFIG","Script/Flow/Flow_Config.lbc")
	Sys.CurrentFlow = "CONFIG"
End

Void Bugs()
	Sys.OpenURL("https://github.com/PhantasarProductions/House-Of-Cards/issues")
End

Void Achievements()
	Sys.LoadNewState("FLOW_ACH","Script/Flow/FLOW_Ach.Lbc")
	Sys.CurrentFlow="ACH"
End

Init
	plua i
	i = New MenuItem("PLAY",Play)
	i = New MenuItem("CONFIG",Config)
	i = New MenuItem("ACHIEVEMENTS",Achievements)
	i = New MenuItem("BUGS",Bugs)
	i = New MenuItem("EXIT",Sys.Exit)
	// Prevent trouble with memory trashing
	MenuItem.Last = nil
End

ReadOnly Var Logo = ObtainImage("LOGO")
Int Hue = os.time()
ReadOnly Var Fnt  = ObtainFont("RYANNA")

Global Void MainFlow()
	Hue = (Hue+1)%360
	If Events.AppTerminate(); Sys.Exit(); End
	DrawBackground()
	Logo.Draw(Graphics.Width div 2,25)
	Plua Item;Item = MenuItem.First
	Repeat
		SetColor(255,255,255)
		If MY>Item.Y && MY<Item.Y+30
			SetColorHSV(360-Hue,1,0.5)
			SetAlpha(127)
			Rect(0,Item.Y,Graphics.Width,30)
			SetColorHSV(Hue,1,1)
			SetAlpha(255)
			If ML && ITEM.Allow()
				ITEM.Action()
			End
		End
		Text("MAINMENU",Item.RTag,Item.X,Item.Y,2)
		Item = Item.Next
	LoopWhile Item
	SetColor(255,255,255)
	Fnt.Text("(c) Jeroen P. Broks",Graphics.Width div 2,Graphics.Height-35,2)
	ShowMouse()
End
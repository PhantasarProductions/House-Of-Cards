// License Information:
// House of Cards
// Black Hole
// 
// 
// 
// (c) Jeroen P. Broks, 2024
// 
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.
// 
// Please note that some references to data like pictures or audio, do not automatically
// fall under this licenses. Mostly this is noted in the respective files.
// 
// Version: 24.04.09
// End License Information
Script

#region "Use"
#use "Libs/XMath"
#use "Libs/SCI/Sys"
#use "Libs/SCI/GINIE"
#use "Libs/SCI/Graphics"
#use "Script/Use/Lnk/Deck"
#use "Script/Use/Lnk/Card"
#use "Script/Use/Gen/Ach"
#use "Script/Use/Gen/Muis"
#use "Script/Use/Gen/Sound"
#use "Script/Use/Gen/RText"
#use "Script/Use/Gen/Config"
#use "Script/Use/Gen/QCardWrite"
#use "Script/Use/Gen/GameFeatures"
#endregion

#region "Base Vars"
ReadOnly Var ICO = LoadNewImage("Script/Game/Black Hole/Icon.png","ICON_BLACKHOLE")
ReadOnly Var BCK = LoadNewImage("GFX/Backgrounds/Stretch/Black Hole.jpg","BACKGROUND_BLACKHOLE")
ReadOnly Var PPP = LoadNewImage("Script/Game/Spider/PointPackages.png","SPIDER_PPP") // I really should not have put this to Spider alone... Oh well.
ReadOnly Var OKE = ObtainImage("OK")
ReadOnly Int DL4 = Graphics.Width div 4
Int HUE
#endregion

#region "Cards & Nodes"
ReadOnly Var BHL_StatePics = { [1] = ObtainImage("SUCCESS"), [2] = ObtainImage("FAIL") }
ReadOnly Var BHL_Config = GINIE.Obtain("CONFIG::BLACKHOLE")
Var BHL_Deck
Int BHL_State

Class CardNode
	Static ReadOnly Var SZ = Lua.Scyndi.Class.CardSize.FromH(Graphics.Height div 5)
	Static Var CD	
	Static ReadOnly Var Setje = {}
	Static Var Center
	Static Get Int W; Return SZ.W; End
	Static Get Int H; Return SZ.H; End
	Static Bool QuickCenter = False
	ReadOnly Int X
	ReadOnly Int Y
	ReadOnly Int CX
	ReadOnly Int CY
	ReadOnly Table Cards

		
	Constructor(Int _X, Int _Y)
		X = _X
		Y = _Y
		CX = _X + (W div 2)
		CY = _Y + (H div 2)
		Cards = {}
	End
	
	Static Void Add(Int _X,Int _Y)
		Append(Setje,New CardNode(_X,_Y))
	End
	
	Static Void DrawNodes()
		For n in Each(Setje)
			SetColorHSV(260,1,os.time() % 2)
			Rect(n.X,n.Y,n.W,n.H,1)
		End
		SetColorHSV(Hue,1,1)
		Rect(Center.X,Center.Y,Center.W,Center.H,1)
	End
End

Class BHCard
	ReadOnly Int DegSpeed = 5
	ReadOnly Number RadSpeed = 4.25
	Int GX
	Int GY
	Int X
	Int Y
	Number Radius
	Int Degrees
	Bool SuckingIn = False
	ReadOnly Var Crd
	Get Int Suit; Return Crd.CardSuit; End
	Get Int Val; Return Crd.CardValue; End
	Static Get Int W; Return CardNode.SZ.W; End
	Static Get Int H; Return CardNode.SZ.H; End	
	Get Int CX; Return X+(W div 2); End
	Get Int CY; Return Y+(H div 2); End

	
	Void Suck()
		// Thank you Pythagoras, for solving the easy part.
		Radius = math.sqrt( (math.abs(CX-CardNode.Center.CX)^2) + ( math.abs(CY-CardNode.Center.CY)^2 ) )
		
		// Dirty and slow, but it works (I hope)
		plua md; md = Radius
		Degrees = 0
		For i=0,360
			plua _x
			plua _y
			plua _d
			_x = CardNode.Center.CX+(xmath.sin(i)*Radius)
			_y = CardNode.Center.CX+(xmath.cos(i)*Radius)
			_d = math.sqrt( (math.abs(_x-CX)^2) + (math.abs(_y-CY)^2) )
			if (_d<md)
				Degrees=i
				md=_d
			End
		End
		
		
		CSayF("Sucking %d:%02d - Radius: %05.1f - Angle: %03d",Suit,Val,Radius,Degrees) 
		
		GX = CardNode.Center.CX
		GY = CardNode.Center.CY
		SuckingIn = True
		CardNode.QuickCenter = False
	End
	
	Void Draw()
		If SuckingIn			
			Degrees = (Degrees + DegSpeed) % 360
			Radius  = math.max(0,Radius-RadSpeed)
			X = (CardNode.Center.CX + math.floor( Radius*xmath.sin(Degrees) )) - (W div 2) 
			Y = (CardNode.Center.CY + math.floor( Radius*xmath.cos(Degrees) )) - (H div 2) 
			if Radius==0
				SuckingIn = False
				GX = CardNode.Center.X
				GY = CardNode.Center.Y				 
				X = GX
				Y = GY
			End
			//CSayF("Sucking in - Radius: %5.1f; Angle: %d -> (%d,%d)",Radius,Degrees,X,Y)
		Else
			if X>GX; X = x - math.max(1,math.abs(X-GX) div 10); End
			if Y>GY; Y = Y - math.max(1,math.abs(Y-GY) div 10); End
			if X<GX; X = x + math.max(1,math.abs(GX-X) div 10); End
			if Y<GY; Y = Y + math.max(1,math.abs(GY-Y) div 10); End
		End
		CardNode.CD.DrawCard(X,Y,Suit,Val)
	End
	
	Get Var CenterCard
		Return CardNode.Center.Cards[Len(CardNode.Center.Cards)-1]
	End
	
	Get bool Allow		
		switch Val
			case  1; return CenterCard.Val==2 || CenterCard.Val==13
			case 13; return CenterCard.Val==1 || CenterCard.Val==12
			default; return math.abs(CenterCard.Val-Val)==1
		end
	End
	
	Constructor(C)
		Crd = C
		Y = Graphics.H + 100
		X = math.random(-100,Graphics.Width+100)
	End
End	
	

Init
	For r=0,3
		Int Y=math.floor( (Graphics.Height * 0.10) + (r*(CardNode.H*1.125) ) )
		For c=0,4
			Int X = math.floor( (Graphics.Width * 0.10) + (c * (Graphics.Width/6) ) )
			Bool Allow
			Switch r
				case 0;   Allow=true
				case 1 2; Allow=c!=2
				case 3;   Allow=c<4
				default;  Lua.error(sprintf("Invalid node row: %d",r))
			End
			If Allow; CardNode.Add(X,Y); End
		End			
	End
	CardNode.Center = New CardNode( (Graphics.Width div 2) - (CardNode.SZ.W div 2),(Graphics.Height div 2) - (CardNode.SZ.H div 2) )
End

Void DrawCards()
	Int Count = 0
	Int Allowed = 0
	SetColor(255,255,255)
	For n in Each(CardNode.Setje)
		For i,c in iPairs(n.Cards)
			c.GX=n.X+(i*40)
			c.GY=n.Y+i
			c.Draw()
			Count++
			If c.Allow && i==len(n.Cards)-1
				Allowed++
				If MX>c.x && MY>c.y && MX<c.x+c.w && MY<c.y+c.h
					PPP.Stretch(c.CX-96,c.CY-96,96,96)
					QCardWrite.Show(CardNode.Center.CX,CardNode.Center.CY+c.h,c.suit,c.val)
					If ML
						Append(CardNode.Center.Cards,c)
						c.Suck()
						n.Cards[i]=nil
					End
				end
			End
		End
	End
	if CardNode.QuickCenter
		CardNode.Center.Cards[len(CardNode.Center.Cards)-1].Draw()
	Else
		CardNode.QuickCenter = True
		For c in each(CardNode.Center.Cards)
			c.Draw()
			CardNode.QuickCenter = CardNode.QuickCenter && c.radius==0
		End
	End
	ShowLeft(Count,DL4,2,2)
	ShowAllowed(Allowed,DL4*3,2,2)
	If BHL_State==0
		If Count==0
			SFX("COMPLETE")
			BHL_State=1
		ElIf Allowed==0
			SFX("BUZZ")
			BHL_State=2
		End
	End
End

Void StateFlow()
	If BHL_State==0; Return; End
	BHL_StatePics[BHL_State].Draw(Graphics.Width div 2, Graphics.Height div 2)
	if MY>Graphics.Height-48 && MX>(DL4*2)-96 && MX<(DL4*2)+48
		SetColorHSV(Hue,1,1)
		if ML
			GotoFlow("SELECTGAME")
		End
	End
	OKE.Stretch((DL4*2)-48,Graphics.Height-96,96,96)
End

#region "CallBacks"
Global Bool GameFeatures_SaveExit_Allow(); Return False; End

Global Void GameNew()
	BHL_State = 0
	Bool AceC = Upper(BHL_Config.Value("Config","AceSpades"))=="YES"
	CSayF("Ace of Spades: %s -> %s",BHL_Config.Value("Config","AceSpades"),AceC)
	CardNode.CD = CardNode.SZ.Bind()
	BHL_Deck = Deck.Shuffle("DECK_YUKON",BHL_Config.Value("Shuffle","Shuffle"))
	Int p=0
	for i=0,len(CardNode.Setje)-1;CardNode.Setje[i].Cards = {}; End
	For i=0,50
		if AceC && BHL_Deck[p].CardValue==1 && BHL_Deck[p].CardSuit==3; p++; End 
		Append(CardNode.Setje[i % len(CardNode.Setje)].Cards,new BHCard(BHL_Deck[p]))
		p++
	End
	If AceC
		CardNode.Center.Cards= NewArray(new BHCard(new DeckCard(3,1)))
	Else
		CardNode.Center.Cards= NewArray(New BHCard(BHL_Deck[51]))		
	End
	CardNode.Center.Cards[0].Suck()
End

Global Void MainFlow()
	Hue = (Hue+1)%360
	SetColor(255,255,255)
	BCK.Stretch(0,0,Graphics.Width,Graphics.Height)
	ICO.Stretch(Graphics.Width-96,Graphics.Height-96,96,96)
	CardNode.DrawNodes()
	DrawCards()
	GameFeatures.Button()
	StateFlow()
	ShowMouse()	
End
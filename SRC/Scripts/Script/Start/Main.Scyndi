// License Information:
// House Of Cards
// Start It All Up
// 
// 
// 
// (c) Jeroen P. Broks, 2024
// 
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.
// 
// Please note that some references to data like pictures or audio, do not automatically
// fall under this licenses. Mostly this is noted in the respective files.
// 
// Version: 24.04.03
// End License Information

// Main

Script

#use "Libs/SCI/Sys"
#use "Libs/SCI/Audio"
#use "Libs/SCI/GINIE"
#use "Libs/SCI/Medals"
#use "Libs/SCI/SString"
#use "Libs/SCI/Graphics"
#use "Libs/SCI/InterVar"

#use "Libs/Rosetta"

PLUA CFG
PlUA RYA

Class ToLoad
	Static ReadOnly Table Lijst = {} 
	ReadOnly String Type
	ReadOnly String Tag
	ReadOnly String File
	ReadOnly Table Specifications
	Constructor(string _Type,String _Tag, String _File,Spec)
		Specifications = {}
		For K,V in Pairs(Spec || {});Specifications[Upper(K)]=V;End
		Type=Upper(_Type)
		Tag=Upper(_Tag)
		File=_File		
	End
End

Void Img(String Tag,String File,Hot)	
	ToLoad.Lijst[Len(ToLoad.Lijst)]=New ToLoad("IMAGE",Tag,File,{["Hot"]=Hot})
End

Void Fnt(String Tag,String File)
	ToLoad.Lijst[Len(ToLoad.Lijst)]=New ToLoad("FONT",Tag,File,{["Ryanna"]=Upper(File)=="FONT/RYANNA.JPBF"})
End

Void Aud(String Tag,String File)
	ToLoad.Lijst[Len(ToLoad.Lijst)]=New ToLoad("AUDIO",Tag,File)
End


Void Scr(String Tag,String File)
	ToLoad.Lijst[Len(ToLoad.Lijst)]= New ToLoad("SCRIPT",Tag,File)
End
		

Init
	// Take note of debug situation!
	CSaySetConfig("ALLOW","DEBUGONLY")
		
	
	// Global Config
	CSay("Loading Global Config")
	CFG = GINIE.LoadHome("House Of Cards.GlobalConfig.ini","GLOBALCONFIG","House Of Cards.GlobalConfig.ini","House Of Cards - (c) Jeroen P. Broks")
	
	CSay("Setting up Medals")
	Medals.Game = "House Of Cards"
	
	// Rosetta
	CFG.NewValue("Rosetta","Language","English")
	Rosetta.StringsDir = "Lang"
	
	// Font
	Fnt("RYANNA","Font/Ryanna.jfbf")
	Fnt("MINI","Font/Mini.jfbf")
	
	// Images
	Img("LOGO","GFX/Logo/Main.png","TOPCENTER")
	Img("MOUSE_TRUE","GFX/MOUSE/TRUE.PNG")
	Img("MOUSE_FALSE","GFX/MOUSE/FALSE.PNG")
	Img("SUCCESS","GFX/End Game/Success.PNG","CENTER")
	Img("FAIL","GFX/End Game/Fail.png","CENTER")
	Img("CLEARED","GFX/End Game/Cleared.png","CENTER")
	Img("NOMOREMOVES","GFX/End Game/NoMoreMoves.png","CENTER")
	Img("GAMEOVER","GFX/End Game/GameOver.png","CENTER")
	Img("FUNCTIONS","GFX/Buttons/Functions.png")
	Img("UNDO","GFX/Buttons/Undo.png")
	Img("OK","GFX/Buttons/OK.png")
	For _,I in Pairs{"Up","Down","Left","Right"}
		Img(sprintf("ARROW_%s",Upper(I)),sprintf("GFX/Arrows/%s.png",I))
	End
	
	// Audio
	Aud("BONUS","SFX/Bonus.ogg")
	Aud("COMPLETE","SFX/Complete.ogg")
	Aud("SLIDE","SFX/CardSlide.ogg")
	Aud("BUZZ","SFX/Buzz.ogg")
	
	// Script
	Scr("FLOW_MAINMENU","Script/Flow/Flow_MainMenu.lbc")
End

Int Idx=0
Global Void MainFlow()	
	If Idx>=Len(ToLoad.Lijst)
		Sys.CurrentFlow = "MAINMENU"
		CFG.NewValue("SeedLog","Log","No")
		CSayF("Seedlog: %s",Upper(CFG.Value("SeedLog","Log")))
		If Upper(CFG.Value("SeedLog","Log"))=="YES"			
			CFG.NewValue("Seedlog","Signature",MD5("House of Cards %d %s",os.time(),os.date()))
			CSayF("Seedlog on signature %s has been enabled",CFG.Value("SeedLog","Signature"))
		End
		Return
		//Lua.error("Flow chain to main menu not yet scripted")
	End
	Plua ITM;ITM=ToLoad.Lijst[Idx];Idx++
	CSayF("%03d/%03d: Loading %s: %s",Idx,Len(ToLoad.Lijst),lower(ITM.Type),ITM.File)
	Switch ITM.Type
		Case "IMAGE"
			plua i;i = LoadImage(ITM.FILE,ITM.TAG)
			plua h;h = Upper(ITM.Specifications.HOT || "NOTHING")			
			Switch h
				Case "NOTHING"
				Case "CENTER"
					i.HotCenter()
				Case "TOPCENTER"
					i.Hot(i.Width div 2,0)					
				Default
					CSayF("Unknown Image Hot: %s",h)
			End
		Case "FONT"
			plua f;f = LinkFont(ITM.FILE,ITM.TAG)
			If ITM.Specifications.RYANNA || ITM.Tag=="RYANNA"
				RYA = f
			End
		Case "AUDIO"
			Audio.Load(ITM.FILE,ITM.TAG)
		Case "SCRIPT" "STATE"
			Sys.LoadState(ITM.TAG,ITM.FILE)
		Default; Lua.error("I really don't know how to load a "..ITM.Type)
	End
	If RYA
		String Ltext
		SetColorHSV(math.random(0,360),1,1)
		Ltext = sprintf("Loading %d%%",math.floor( (Idx/Len(ToLoad.Lijst)) * 100 ))
		CSay(Ltext)
		RYA.Text(Ltext,Graphics.Width div 2,Graphics.Height div 2,2,2)
	End
End